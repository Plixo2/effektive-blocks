module color

/*
  API for colors (RGBA8)
*/


/// Color components in RGBA format
/// Each component from 0-255
record Color(r: Int, g: Int, b: Int, a: Int)


def colorFormat { str: => Unit / {literal, splice[Double], splice[Int]}}: String = stringBuffer  {
  try {
    str()
    do flush()
  }
  with literal { s => resume(do write(s)) }
  with splice[Int] { i => 
    val format = i.max(0).min(255).show
    resume(do write(format))
  }
  with splice[Double] { d => 
    val format = (d / 255.0).max(0.0).min(1.0).show
    resume(do write(format))
  }
}

def infixEq(self: Color, other: Color): Bool = 
  self.r == other.r &&
  self.g == other.g &&
  self.b == other.b &&
  self.a == other.a

def infixNeq(self: Color, other: Color): Bool = 
  not(infixEq(self, other))

/// Css color representation 
/// Red, Green, Blue from 0 to 255
/// Alpha from 0 to 1
def cssString(self: Color): String = {
  colorFormat"rgba(${self.r}, ${self.g}, ${self.b}, ${self.a.toDouble})"
}

def show(self: Color): String = {
  colorFormat"Color(${self.r}, ${self.g}, ${self.b}, ${self.a})"
}
